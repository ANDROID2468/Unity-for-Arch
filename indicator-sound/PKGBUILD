# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+17.04.20170109
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf gsettings-ubuntu-schemas libgee libnotify
         libpulse libxml2 pulseaudio qt5-base unity-api unity-control-center)
makedepends=(cmake cmake-extras gobject-introspection intltool vala)
groups=(unity)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch)
sha512sums=('3c76d8808c31d16752828580f6651eb49e78e19968c5407fd00de00fa3ebbe8e9f513e43bc5c43c4438271204fff652e00e5e5d832bd79e10c1646f0b01dc111'
            '3c57829cd55b1aea14b5bc2f66e8b015d28ccdd8a7d56c5d445efb61d149a49159e6be0c267d8eded5884d53361705373837cfe789d39ced9d262a5fa630491f'
            'a2c8b9f23419c8554a38ac6e3128841e38d15e6ca2f1c70a28a01546ca61090059304974a2d5d8ed5e7c9cc68a2762c6b856d575bae3d74e81b45c0b13e517ce')

prepare() {
    patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
    patch -p1 -i ../0002-Remove-tests.patch
}

build() {
    [[ -d build ]] || mkdir build
    pushd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
    popd
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    rm -v "${pkgdir}"/usr/var/lib/polkit-1/localauthority/10-vendor.d/50-com.canonical.indicator.sound.AccountsService.pkla

    rm -rv "${pkgdir}"/usr/share/upstart/
}
