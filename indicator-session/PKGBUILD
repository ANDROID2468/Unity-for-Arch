# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=+17.04.20161021
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="Indicator showing session management, status and user switching"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-session"
license=(GPL)
depends=(gnome-settings-daemon)
makedepends=(cmake intltool python2)
optdepends=('lightdm: support for lightdm')
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-There-is-no-help.patch
        0002-Remove-url-dispatcher-dependency.patch)
sha512sums=('cdc7049ea79820b746fe293bd430b2ed561fcc803e483ccc9d1b5744d92058a9983e4f81bf34190a6829ee389234cdd5b27ffab71e06ed97a9a72211023d9a56'
            'd6fb693580c921ce397124e12222aeff63734b1a2729f9e661a432613178ed8ff8da6107364e151bbd2bd58c3e70e18b71d8b96988025134f0fee8bc03868135'
            '8ab2047eae79c01d206811f76efab57ac55161a0c3ba4f7f9e1fa37c80cc619d9571a42af0259b6e32f6597cbab6bc27fc5c5fb50965737065baae6cb546c025')

prepare() {
    patch -p1 -i ../0001-There-is-no-help.patch
    patch -p1 -i ../0002-Remove-url-dispatcher-dependency.patch
}

build() {
    mkdir build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -Denable_tests=OFF \
        -Denable_lcov=OFF

    make
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    rm -vf "${pkgdir}/test-service"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"

    rm -rv "${pkgdir}"/usr/share/upstart/
}
