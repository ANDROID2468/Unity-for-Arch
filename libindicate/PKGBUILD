# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase}, repo=zesty
# vercheck-launchpad: name=${pkgbase}

pkgbase=libindicate
pkgname=(libindicate libindicate-gtk2 libindicate-gtk3)
pkgver=12.10.1
pkgrel=3
epoch=1
pkgdesc="Library for raising indicators via DBus"
arch=(i686 x86_64)
url="https://launchpad.net/libindicate"
license=(LGPL)
makedepends=(libdbusmenu-glib dbus-glib gtk2 gtk3 gobject-introspection vala
             gtk-sharp-2 pygtk gnome-doc-utils perl-xml-libxml)
groups=(unity)
source=("http://launchpad.net/${pkgbase}/12.10/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz"
        0001-Fix-autotools-errors.patch
        0002-Python-2-fixes.patch
        0003-Remove-mono-bindings.patch)
sha512sums=('4dcb5f292153210bc15a05f117101429da77a60b8b05b569baddf0e9043d852c2dccb0d5a6adbdf4d4a1b6f06b33c61e242bb4bcb376e3d96563bda591685c06'
            '021c71f7c4573fab2d11a9f71c2c7f75d1e9ea7c518a266d64bce04a7dfc5e8fca5467050baac27abd196afb179acf4e01b0d2dc2d09806c8131535a101d2aaa'
            '9ca2663d8fb29e6dfc0232792537191ba4643a2dec989041cba725f9239c76e6b3f69dcc1b26560c6e8590f1e3b8ac49fb4a7c2f37e1f72eac5a7a9dc0198f99'
            '5cf6d9d447e98398081d762457db2b921dc0ae18fc1c4242edb81b29bc2cf2fbe0bdaad3fab20fd2d34a85301a0b4eeb2eb159e05ef4e87cb45e8f6e0d98d1da')

prepare() {
    cd "${pkgbase}-${pkgver}"

    patch -p1 -i ../0001-Fix-autotools-errors.patch
    patch -p1 -i ../0002-Python-2-fixes.patch
    patch -p1 -i ../0003-Remove-mono-bindings.patch

    autoreconf -vfi
}

build() {
    cd "${pkgbase}-${pkgver}"

    export CFLAGS+=" -Wno-error=deprecated-declarations"

    autoreconf -vfi

    [[ -d build-gtk2 ]] || mkdir build-gtk2
    pushd build-gtk2
    ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=2 --disable-static \
        PYTHON=python2 CSC=/usr/bin/mcs
    make -j1
    popd

    [[ -d build-gtk3 ]] || mkdir build-gtk3
    pushd build-gtk3
    ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=3 --disable-static \
        PYTHON=python2 CSC=/usr/bin/mcs
    make -j1
    popd
}

package_libindicate() {
    depends=(libdbusmenu-glib dbus-glib)

    cd "${pkgbase}-${pkgver}/build-gtk2"

    make -j1 -C libindicate DESTDIR="${pkgdir}" install
    #make -j1 -C bindings/mono/indicate DESTDIR="${pkgdir}" install
    make -j1 -C tests DESTDIR="${pkgdir}" install
    make -j1 -C docs DESTDIR="${pkgdir}" install
}

package_libindicate-gtk2() {
    pkgdesc+=" (GTK+ 2 library)"
    depends=(cairo libindicate gtk2 python2-gobject2)

    cd "${pkgbase}-${pkgver}/build-gtk2"

    make -j1 -C libindicate-gtk DESTDIR="${pkgdir}" install
    make -j1 -C bindings/python DESTDIR="${pkgdir}" install
    #make -j1 -C bindings/mono/indicate-gtk DESTDIR="${pkgdir}" install
    make -j1 -C examples DESTDIR="${pkgdir}" install
}

package_libindicate-gtk3(){
    pkgdesc+=" (GTK+ 3 library)"
    depends=(cairo libindicate gtk3)

    cd "${pkgbase}-${pkgver}/build-gtk3"

    make -j1 -C libindicate-gtk DESTDIR="${pkgdir}" install
}
